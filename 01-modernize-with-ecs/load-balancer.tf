resource "aws_security_group" "lb_sg" {
  name = "${var.cluster_name}-alb-sg"

  vpc_id = local.vpc_id
  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    from_port   = 443
    to_port     = 443
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "${var.cluster_name}-alb-sg"
  }
}

resource "aws_security_group" "service_sg" {
  depends_on = [
    aws_security_group.lb_sg
  ]

  name   = "${var.cluster_name}-svc-sg"
  vpc_id = local.vpc_id

  ingress {
    from_port       = 0
    to_port         = 65535
    protocol        = "tcp"
    cidr_blocks     = [local.vpc_cidr_block]
    security_groups = [aws_security_group.lb_sg.id]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  lifecycle {
    ignore_changes = [
      ingress,
      egress
    ]
  }

  tags = {
    Name = "${var.cluster_name}-svc-sg"
  }
}

resource "aws_lb" "cluster" {
  name     = "${var.cluster_name}-alb"
  internal = false

  load_balancer_type         = "application"
  enable_deletion_protection = false

  security_groups = [
    aws_security_group.lb_sg.id
  ]
  subnets = local.public_subnet_ids

  access_logs {
    bucket  = aws_s3_bucket_policy.s3_lb_logs.id
    prefix  = "${var.cluster_name}-public-alb"
    enabled = true
  }

  tags = {
    Name = "${var.cluster_name}-alb"
  }
}

resource "aws_lb_listener" "ecs_listener" {
  load_balancer_arn = aws_lb.cluster.arn

  port     = "80"
  protocol = "HTTP"

  # HTTPS mode: redirect 80 -> 443
  dynamic "default_action" {
    for_each = local.use_https ? [1] : []
    content {
      type = "redirect"

      redirect {
        port        = "443"
        protocol    = "HTTPS"
        status_code = "HTTP_301"
      }
    }
  }

  # HTTP-only mode: this listener receives the service rules
  dynamic "default_action" {
    for_each = local.use_https ? [] : [1]
    content {
      type = "fixed-response"
      fixed_response {
        content_type = "application/json"
        message_body = "{\"message\": \"Not Found!!\"}"
        status_code  = "404"
      }
    }
  }

  tags = {
    Name = "ecs-http-listener"
  }
}

resource "aws_lb_listener" "ecs_listener_443" {
  count = local.use_https ? 1 : 0

  load_balancer_arn = aws_lb.cluster.arn

  port     = "443"
  protocol = "HTTPS"

  ssl_policy      = "ELBSecurityPolicy-2016-08"
  certificate_arn = aws_acm_certificate.acm[0].arn

  default_action {
    order = 1
    type  = "fixed-response"
    fixed_response {
      content_type = "application/json"
      message_body = "{\"message\": \"Not Found!!\"}"
      status_code  = "404"
    }
  }

  tags = {
    Name = "ecs-https-listener"
  }
}

resource "aws_route53_record" "alb_record" {
  count = local.use_https ? 1 : 0

  zone_id = data.aws_route53_zone.selected[0].zone_id

  name = "myapp.${var.service_domain}"
  type = "A"

  alias {
    name    = aws_lb.cluster.dns_name
    zone_id = aws_lb.cluster.zone_id

    evaluate_target_health = true
  }
}
